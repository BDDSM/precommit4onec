Перем ЛокальныеНастройки;
Перем ГлобальныеНастройки;
Перем ИспользуютсяЛокальныеНастройки;

Функция ПолучитьНастройки(КаталогРепозитория, ВернутьГлобальныеЕслиНетЛокальных = Истина) Экспорт
	
	Если ВернутьГлобальныеЕслиНетЛокальных Тогда
		
		ГлобальныеНастройки();
		
	КонецЕсли;
	
	Лог = МенеджерПриложения.ПолучитьЛог();
	
	ЛокальныеНастройки = Новый НастройкиРепозитория(КаталогРепозитория);
	
	ИспользуютсяЛокальныеНастройки = ЕстьНастройкиPrecommt4onec(ЛокальныеНастройки) ИЛИ НЕ ВернутьГлобальныеЕслиНетЛокальных;
	
	Если ИспользуютсяЛокальныеНастройки Тогда
		
		ВозвращаемаяНастройка = ЛокальныеНастройки;

	Иначе
	
		ВозвращаемаяНастройка = ГлобальныеНастройки;
		
	КонецЕсли;

	Возврат ВозвращаемаяНастройка;
	
КонецФункции

Функция ГлобальныеНастройки() Экспорт

	
	Если ГлобальныеНастройки = Неопределено Тогда
	
		ГлобальныеНастройки = Новый НастройкиРепозитория(МенеджерПриложения.ПутьКРодительскомуКаталогу());
		
	КонецЕсли;
	
	Возврат ГлобальныеНастройки;
	
КонецФункции

Функция ЗначениеНастройки(КлючНастройки, Подпроект = Неопределено, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Если ИспользуютсяЛокальныеНастройки Тогда

		Значение = ПолучитьЗначениеНастройки(ЛокальныеНастройки, КлючНастройки, Подпроект);

	Иначе

		Значение = ПолучитьЗначениеНастройки(ГлобальныеНастройки, КлючНастройки, Подпроект);

	КонецЕсли;
	
	Возврат ?(Значение = Неопределено, ЗначениеПоУмолчанию, Значение);
		
КонецФункции

Функция ИменаЗагружаемыхСценариев(Подпроект = "") Экспорт

	ИменаИсключаемыхСценариев = ЗначениеНастройки("ОтключенныеСценарии", Подпроект);
	
	ГлобальныеСценарии = ЗначениеНастройки("ГлобальныеСценарии", Подпроект);
	
	Если НЕ ЗначениеЗаполнено(ГлобальныеСценарии) Тогда
		
		ГлобальныеСценарии = ПолучитьЗначениеНастройки(ГлобальныеНастройки, "ГлобальныеСценарии");

	КонецЕсли;

	ИменаЗагружаемыхСценариев = Новый Массив;
	
	Если ИменаИсключаемыхСценариев = Неопределено Тогда
		
		ИменаИсключаемыхСценариев = Новый Массив();
		
	КонецЕсли;
	
	Для Каждого ИмяСценария Из ГлобальныеСценарии Цикл
		
		Если ИменаИсключаемыхСценариев.Найти(ИмяСценария) = Неопределено Тогда
			
			ИменаЗагружаемыхСценариев.Добавить(ИмяСценария);

		КонецЕсли;
		
	КонецЦикла;

	Возврат ИменаЗагружаемыхСценариев;
	
КонецФункции

Функция ПолучитьСписокИсполяемыхСценариев(Знач ГлобальныеСценарии, Знач ОтключенныеСценарии) Экспорт

	ИменаЗагружаемыхСценариев = Новый Массив;
	
	Если ОтключенныеСценарии = Неопределено Тогда
		
		ОтключенныеСценарии = Новый Массив();
		
	КонецЕсли;
	
	Для Каждого ИмяСценария Из ГлобальныеСценарии Цикл
		
		Если ОтключенныеСценарии.Найти(ИмяСценария) = Неопределено Тогда
			
			ИменаЗагружаемыхСценариев.Добавить(ИмяСценария);

		КонецЕсли;
		
	КонецЦикла;

	Возврат ИменаЗагружаемыхСценариев;

КонецФункции

Функция КлючНастройкиPrecommt() Экспорт
	
	Возврат "Precommt4onecСценарии";
	
КонецФункции

Функция ИспользуютсяЛокальныеНастройки() Экспорт
	
	Возврат ИспользуютсяЛокальныеНастройки;

КонецФункции

Функция ЕстьНастройкиPrecommt4onec(Настройка)
	
	Возврат НЕ Настройка.ЭтоНовый() И Настройка.НастройкиПриложения(КлючНастройкиPrecommt()).Количество();

КонецФункции

Функция НастройкиПроекта(Настройки, Подпроект)
	
	Если ЗначениеЗаполнено(Подпроект) Тогда
		
		НастройкаПодпроекта = Настройки.Настройка(СтрШаблон("%1\%2", КлючНастройкиPrecommt(), Подпроект));
		
		Если НастройкаПодпроекта <> Неопределено Тогда
		
			Возврат НастройкаПодпроекта;
			
		КонецЕсли;

	КонецЕсли;
	
	Возврат Настройки.НастройкиПриложения(КлючНастройкиPrecommt());

КонецФункции

Функция ПолучитьЗначениеНастройки(Настройка, КлючНастройки, Подпроект = Неопределено)
	
	Значение = НастройкиПроекта(Настройка, Подпроект)[КлючНастройки];

	Возврат Значение;
		
КонецФункции