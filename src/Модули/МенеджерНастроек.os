Перем ЛокальныеНастройки;
Перем ГлобальныеНастройки;
Перем ИспользуютсяЛокальныеНастройки;

Перем Проекты;

///////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////

Функция НастройкиРепозитория(КаталогРепозитория, ВернутьГлобальныеЕслиНетЛокальных = Истина) Экспорт
	
	Если ВернутьГлобальныеЕслиНетЛокальных Тогда
		
		ГлобальныеНастройки();
		
	КонецЕсли;
	
	Лог = МенеджерПриложения.ПолучитьЛог();
	
	ЛокальныеНастройки = Новый НастройкиРепозитория(КаталогРепозитория);
	
	ИспользуютсяЛокальныеНастройки = ЕстьНастройкиPrecommt4onec(ЛокальныеНастройки) ИЛИ НЕ ВернутьГлобальныеЕслиНетЛокальных;
	
	Если ИспользуютсяЛокальныеНастройки Тогда
		
		ВозвращаемаяНастройка = ЛокальныеНастройки;

	Иначе
	
		ВозвращаемаяНастройка = ГлобальныеНастройки;
		
	КонецЕсли;

	Проекты = Неопределено;

	Возврат ВозвращаемаяНастройка;
	
КонецФункции

Функция ГлобальныеНастройки() Экспорт

	
	Если ГлобальныеНастройки = Неопределено Тогда
	
		ГлобальныеНастройки = Новый НастройкиРепозитория(МенеджерПриложения.ПутьКРодительскомуКаталогу());
		Проекты = Неопределено;

	КонецЕсли;
	
	Возврат ГлобальныеНастройки;
	
КонецФункции

Функция ЗначениеНастройки(КлючНастройки, Подпроект = Неопределено, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Если ИспользуютсяЛокальныеНастройки Тогда

		Значение = ЗначениеПоКлючу(ЛокальныеНастройки, КлючНастройки(Подпроект, КлючНастройки));

	Иначе

		Значение = ЗначениеПоКлючу(ГлобальныеНастройки, КлючНастройки(Подпроект, КлючНастройки));

	КонецЕсли;
	
	Возврат ?(Значение = Неопределено, ЗначениеПоУмолчанию, Значение);
		
КонецФункции

Функция ИменаЗагружаемыхСценариев(Подпроект = "") Экспорт

	ИменаИсключаемыхСценариев = ЗначениеНастройки("ОтключенныеСценарии", Подпроект);
	
	ГлобальныеСценарии = ЗначениеНастройки("ГлобальныеСценарии", Подпроект);
	
	Если ГлобальныеСценарии = Неопределено Тогда
		
		ГлобальныеСценарии = ПолучитьЗначениеНастройки(ГлобальныеНастройки, "ГлобальныеСценарии");

	КонецЕсли;

	ИменаЗагружаемыхСценариев = Новый Массив;
	
	Если ИменаИсключаемыхСценариев = Неопределено Тогда
		
		ИменаИсключаемыхСценариев = Новый Массив();
		
	КонецЕсли;
	
	Для Каждого ИмяСценария Из ГлобальныеСценарии Цикл
		
		Если ИменаИсключаемыхСценариев.Найти(ИмяСценария) = Неопределено Тогда
			
			ИменаЗагружаемыхСценариев.Добавить(ИмяСценария);

		КонецЕсли;
		
	КонецЦикла;

	Возврат ИменаЗагружаемыхСценариев;
	
КонецФункции

Функция ПолучитьСписокИсполняемыхСценариев(Знач ГлобальныеСценарии, Знач ОтключенныеСценарии) Экспорт

	ИменаЗагружаемыхСценариев = Новый Массив;
	
	Если ОтключенныеСценарии = Неопределено Тогда
		
		ОтключенныеСценарии = Новый Массив();
		
	КонецЕсли;
	
	Для Каждого ИмяСценария Из ГлобальныеСценарии Цикл
		
		Если ОтключенныеСценарии.Найти(ИмяСценария) = Неопределено Тогда
			
			ИменаЗагружаемыхСценариев.Добавить(ИмяСценария);

		КонецЕсли;
		
	КонецЦикла;

	Возврат ИменаЗагружаемыхСценариев;

КонецФункции

Функция КлючНастройкиPrecommit() Экспорт
	
	Возврат "Precommt4onecСценарии";
	
КонецФункции

Функция ИспользуютсяЛокальныеНастройки() Экспорт
	
	Возврат ИспользуютсяЛокальныеНастройки = Истина;

КонецФункции

Функция ПроектыКонфигурации() Экспорт
	
	Если Проекты <> Неопределено Тогда
		
		Возврат Проекты;
		
	КонецЕсли;

	Проекты = Новый Массив;

	БлокПроекты = ЗначениеНастройки("Проекты");
	
	Если ЗначениеЗаполнено(БлокПроекты) Тогда
		
		Для Каждого Элемент Из БлокПроекты Цикл
			
			Проекты.Добавить(Элемент.Ключ);
						
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат Проекты;
	
КонецФункции

Функция НастройкиПроекта(Подпроект) Экспорт
	
	Если ИспользуютсяЛокальныеНастройки Тогда

		Значение = ПолучитьНастройкиПроекта(ЛокальныеНастройки, Подпроект);

	Иначе

		Значение = ПолучитьНастройкиПроекта(ГлобальныеНастройки, Подпроект);

	КонецЕсли;
	
	Возврат Значение;

КонецФункции

Функция НастройкаДляФайла(Знач ОтносительноеИмяФайла) Экспорт
	
	Если СтрНачинаетсяС(ОтносительноеИмяФайла, "/") ИЛИ СтрНачинаетсяС(ОтносительноеИмяФайла, "\") Тогда
		
		ОтносительноеИмяФайла = Сред(ОтносительноеИмяФайла, 2);
		
	КонецЕсли;
	
	ОтносительноеИмяФайла = СтрЗаменить(НРег(ОтносительноеИмяФайла), "\", "/");

	Для Каждого ИмяПроекта Из ПроектыКонфигурации() Цикл
		
		НормализованноеИмяФайла = Лев(ОтносительноеИмяФайла, СтрДлина(ИмяПроекта));

		НормализованноеИмяПроекта = СтрЗаменить(НРег(ИмяПроекта), "\", "/");
		
		Если НормализованноеИмяПроекта = НормализованноеИмяФайла Тогда
			
			Возврат НастройкиПроекта(ИмяПроекта);

		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НастройкиПроекта("");
	
КонецФункции

///////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ФУНКЦИОНАЛ
///////////////////////////////////////////////////////////////////

Функция ПолучитьИменаСценариевКаталога(КаталогСценариев) Экспорт
	
	НайденныеСценарии = Новый Массив;
	ФайлыСценариев = НайтиФайлы(КаталогСценариев, "*.os");
	Для Каждого ФайлСценария Из ФайлыСценариев Цикл
		
		Если СтрСравнить(ФайлСценария.ИмяБезРасширения, "ШаблонСценария") = 0 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		НайденныеСценарии.Добавить(ФайлСценария.Имя);
		
	КонецЦикла;
	
	Возврат НайденныеСценарии;
	
КонецФункции
Функция КлючНастройки(Проект = Неопределено, КлючНастройки = Неопределено)
	
	Ключ = Новый Массив();

	Если ЗначениеЗаполнено(Проект) И ЕстьПроект(Проект) Тогда
		
		Ключ.Добавить("Проекты");
		Ключ.Добавить(Проект);
		
	КонецЕсли;

	Если ЗначениеЗаполнено(КлючНастройки) Тогда
		
		Ключ.Добавить(КлючНастройки);
		
	КонецЕсли;

	Возврат Ключ;
		
КонецФункции

Функция ЕстьПроект(Проект)
	
	Возврат ПроектыКонфигурации().Найти(Проект) <> Неопределено;
	
КонецФункции

Функция ПолучитьНастройкиПроекта(Настройка, Подпроект) Экспорт
	
	Значение = ЗначениеПоКлючу(Настройка, КлючНастройки(Подпроект));
	
	Если Значение = Неопределено Тогда
		
		Значение = ЗначениеПоКлючу(Настройка, "");
		
	КонецЕсли;

	Возврат Значение;

КонецФункции

Функция ЗначениеПоКлючу(Настройка, КлючНастройки)
	
	Значение = Настройка.НастройкиПриложения(КлючНастройкиPrecommit());
	
	Если НЕ ЗначениеЗаполнено(КлючНастройки) Тогда
		
		Возврат Значение;
		
	ИначеЕсли ТипЗнч(КлючНастройки) = Тип("Строка") Тогда
		
		Ключи = СтрРазделить(КлючНастройки, "\");
		
	Иначе
		
		Ключи = КлючНастройки;
		
	КонецЕсли;

	Для каждого Ключ Из Ключи Цикл
		
		Значение = Значение.Получить(Ключ);
		
		Если Значение = Неопределено Тогда
			
			Прервать;
			
		КонецЕсли;

	КонецЦикла;
	
	Возврат Значение;
	
КонецФункции

Функция ЕстьНастройкиPrecommt4onec(Настройка)
	
	Возврат НЕ Настройка.ЭтоНовый() И ЗначениеПоКлючу(Настройка, "").Количество();

КонецФункции

Функция ПолучитьЗначениеНастройки(Настройка, КлючНастройки, Подпроект = Неопределено)
	
	Значение = ЗначениеПоКлючу(Настройка, КлючНастройки(Подпроект, КлючНастройки));

	Возврат Значение;
		
КонецФункции